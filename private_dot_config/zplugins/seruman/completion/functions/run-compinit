#!/bin/zsh

if [[ -n "$1" ]]; then
  ZSH_COMPDUMP="${1:A}"
else
  ZSH_COMPDUMP="${${ZSH_COMPDUMP:-${XDG_CACHE_HOME:=$HOME/.cache}/zsh/compdump}:A}"
fi
mkdir -p ${ZSH_COMPDUMP:h}

# Load and initialize the completion system ignoring insecure directories with a
# cache time of 20 hours, so it should almost always regenerate the first time a
# shell is opened each day.
autoload -Uz compinit
local compfiles=($ZSH_COMPDUMP(Nmh-20))
if (( $#compfiles )); then
  compinit -i -C -d "$ZSH_COMPDUMP"
else
  compinit -i -d "$ZSH_COMPDUMP"
  # Keep $ZSH_COMPDUMP younger than cache time even if it isn't regenerated.
  touch "$ZSH_COMPDUMP"
fi

# Compile compdump, if modified, in background to increase startup speed.
(
  if [[ -s "$ZSH_COMPDUMP" && (! -s "${ZSH_COMPDUMP}.zwc" || "$ZSH_COMPDUMP" -nt "${ZSH_COMPDUMP}.zwc") ]]; then
    zcompile "$ZSH_COMPDUMP"
  fi
) &!
