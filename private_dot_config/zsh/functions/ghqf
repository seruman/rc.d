# vi: ft=zsh

local previewcmd
read -r -d '' previewcmd <<'EOF'
setopt extended_glob
setopt no_nomatch

export CLICOLOR_FORCE=1
export COLORTERM=truecolor

local repo=$(echo {})
local repo_path=$(ghq list --full-path -e {})

local current_branch=$(git -C ${repo_path} branch --show-current)
local ref_name=$(git -C ${repo_path} name-rev --name-only HEAD)

local ref_to_show=${current_branch:-$ref_name}

gum format -t template "{{ Foreground (Color \"31\" )  ( Bold \"${repo}\")}} @{{ Bold \"${ref_to_show}\" }}"
echo '\n'

local remotes=($(git -C "${repo_path}" remote))

for remote in ${remotes[@]}; do
	local url=$(git -C ${repo_path} remote get-url ${remote})
	gum format -t template "{{ Bold \"${remote}\" }}: {{ Underline \"${url}\" }}"
	echo
done

local readme_files=$(ls ${repo_path}/(#i)readme.md  2>/dev/null)
if [ -n "$readme_files" ]; then
    glow -s light $readme_files
fi
EOF

local repopath=$(
	ghq list |
		fzf \
			--ansi \
			--prompt 'Repositories> ' \
			--preview "${previewcmd}" \
			--query "${*:-}" |
		xargs -I{} ghq list --full-path -e {}
)
if [ -n "$repopath" ]; then
	cd "$repopath"
fi
